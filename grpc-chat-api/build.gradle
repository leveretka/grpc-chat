group = 'ua.nedz.demo'
version = '1.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://marharyta.jfrog.io/marharyta/libs-snapshots-gradle'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'
apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

repositories {
    jcenter()
    mavenCentral()
}

def grpcVersion = '1.14.0' // CURRENT_GRPC_VERSION

dependencies {
    compile "com.google.api.grpc:proto-google-common-protos:1.0.0"
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.5.1-1' }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"}
        //grpc { artifact = "io.grpc:protoc-gen-grpc-js:${grpcVersion}"}
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'libs-snapshots-local-gradle'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
    }
    resolve {
        repository {
            repoKey = 'libs-snapshots-gradle'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}


